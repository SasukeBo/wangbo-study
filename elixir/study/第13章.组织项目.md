# 组织项目

组织源代码、编写测试和处理依赖关系，遵循Elixir习惯，获得工具的支持。

本章会介绍Elixir构建工具mix。介绍它使用的目录结构，并介绍如何管理外部依赖。

## 项目：从GitHub获取issue

github提供了一个获取issue的很棒的API。只要向https://api.github.com/repos/user/project/issues 发出
Get请求，就会得到一个issue的Json列表。我们的项目就是对Json列表重新进行格式化，并筛选出最旧的n个，
将结果以表格展示出来：

|  #  |       created_at     | title
| :-: | :------------------: | :-----:
| 889 | 2013-03-16T22:03:13Z | MIX_PATH environment variable (of sorts)
| 892 | 2013-03-20T19:22:07Z | Enhanced mix test --cover
| 893 | 2013-03-21T06:23:00Z | mix test time reports
| 898 | 2013-03-23T19:19:08Z | Add mix compile --warnings-as-errors

### 我们的代码将如何工作

## 任务：用mix来创建我们的新项目

mix是管理Elixir项目的命令行工具，用来创建新项目、管理项目依赖关系、做测试和运行项目代码。

试着运行一下mix：

```
$ mix help
```

### 创建项目树

每个Elixir项目都拥有自己的目录树，如果使用mix来管理这棵树，就需要遵循mix的约定。

```
$ mix new issues
```

`lib/` 存放项目源代码。

`mix.exs` 此源文件包含我们的项目配置选项，在项目开发的过程中，我们会往这里添加东西。

## 转换：解析命令行

我们不用将命令行选项的处理耦合到程序主体中，所以我们编写一个独立的模块来作为用户输入和程序的主体接口。按照约定，这个模块被命名为Project.CLI，在我们的学习项目中是命名为issues.CLI，这个module的主入口点是一个名为run的函数，它接受一个命令行参数数组。

* 约定：在lib/目录创建一个与项目同名的子目录，这个目录会包含我们应用程序的主要源代码，每个文件放一个module。并且每个模块的命名空间都在issues模块之内——模块命名遵循目录命名。

    所以issues.CLI也就是issues模块内嵌的CLI模块。

CLI模块代码:

```elixir
defmodule Issues.CLI do
  @default_count 4

  @moduledoc """
  Handle the command line parsing and the dispatch to
  the various functions that end up generating
  a table of the last _n_ issues in a github project
  """

  def run(argv) do
    parse_args(argv)
  end

  @doc """
  `argv` can be -h or --help, which returns :help.
  Otherwise it is a github user name, project name, and (optionally)
  the number of entries to format.
  Return a tuple of `{ user, project, count }`, or `:help` if help was given.
  """

  def parse_args(argv) do
    parse = OptionParser.parse(argv, switches: [help: :boolean], aliases: [h: :help])

    case parse do
      {[help: true], _, _} ->
        :help

      {_, [user, project, count], _} ->
        {user, project, count}

      {_, [user, project], _} ->
        {user, project, @default_count}

      _ ->
        :help
    end
  end
end
```

## 编写一些基本的测试

Elixir 自带了一个名为ExUnit的简单好用的测试框架。

```elixir
defmodule IssuesTest do
  use ExUnit.Case
  doctest Issues

  test "greets the world" do
    assert Issues.hello() == :world
  end
end
```
它可被用作编写测试文件的模板。

我们为CLI模块编写测试，将这些测试放在test/cli_test.exs文件中。

```elixir
defmodule CliTest do
  use ExUnit.Case
  import Issues.CLI, only: [parse_args: 1]

  test ":help returned by option parsing with -h and --help options" do
    assert parse_args(["-h", "anything"]) == :help
    assert parse_args(["--help", "anything"]) == :help
  end

  test "three values returned if three given" do
    assert parse_args(["user", "project", "99"]) == {"user", "project", 99}
  end

  test "count is defaulted if two values given" do
    assert parse_args(["user", "project"]) == {"user", "project", 4}
  end
end
```
这些测试全部使用ExUnit提供的assert宏。

在issues项目根目录下运行`mix test`，就可以得到测试结果。

## 转换：从GitHub获取数据

解析参数之后，我们要通过从GitHub获取的数据来转换它们。

扩展run函数，调用process函数，向它传入parse_args函数的返回值。

```elixir
  def run(argv) do
    argv
      |> parse_args
      |> process
  end
```

我们需要两种形式的process函数：
* 处理用户请求帮助且parse_args返回:help的情况
* 处理要返回用户、项目和计数值的情况

```elixir
  def process(:help) do
    IO.puts """
    usage: issues <user> <project> [count | #{@default_count}]
    """
    System.halt(0)
  end

  # 此处的GithubIssues模块还没有定义，所以不会生效
  def process({user, project, _count}) do
    Issues.GithubIssues.fetch(user, project)
  end
```

使用mix来运行函数。
```
$ mix run -e 'Issues.CLI.run(["-h"])'
```

现在我们来编写GithubIssues那部分代码，我们的项目会充当HTTP客户端，通过web API访问github。所以我们需要一个外部库。

## 任务：使用外部库

### 寻找库

* 第一个地方是 http://elixir-lang.org/docs/ , 里面都是Elixir文档。
* 检查是否有标准的Erlang库提供了需要的功能。http://erlang.org/doc/
* 如果没有找到需要的库，就要添加外部依赖，例如 http://hex.pm ，Elixir/Erlang包管理器。

### 为项目添加库

mix认为所有的外部库都应该复制到项目的目录结构中。只需列出依赖，mix将会处理这些库。

在mix.exs文件中，为deps函数添加新的依赖。添加HTTPosion库。

```elixir
  defp deps do
    [
      # {:dep_from_hexpm, "~> 0.3.0"},
      # {:dep_from_git, git: "https://github.com/elixir-lang/my_dep.git", tag: "0.1.0"},
      {:httposion, "~> 0.4"}
    ]
  end
```

使用mix deps列出依赖项和它们的状态

```
$ mix deps
```
通过mix deps.get下载依赖项

```
$ mix deps.get
```
再次运行`mix deps`

我们要编写GithubIssues.fetch函数来将用户名和项目转换成包含项目issue的数据结构。

编写一个新模块，Issue.GithubIssues:

```elixir
defmodule Issues.GithubIssues do
  @user_agent [{"User-agent", "Elixir dave@pragprog.com"}]

  def fetch(user, project) do
    issues_url(user, project)
    |> HTTPoison.get(@user_agent)
    |> handle_response
  end

  def issues_url(user, project) do
    "https://api.github.com/repos/#{user}/#{project}/issues"
  end

  def handle_response(%{status_code: 200, body: body}), do: {:ok, body}

  def handle_response(%{status_code: ___, body: body}), do: {:error, body}
end
```

我们可以使用mix.exs中的application函数来启动HTTPoison：

```elixir
  def application do
    [
      extra_applications: [:logger, :httpoison]
    ]
  end
```

我们可以在iex中试试这个，使用`-S mix`选项来运行mix。

```
$ iex -S mix
iex> Issues.GithubIssues.fetch("elixir-lang", "elixir")
```

## 转换响应内容

我们需要一个JSON库来将响应内容转换成数据结构。搜索hex.pm，找到Erlang库的jsx，
把它的依赖加到mix.exs文件

```elixir
{:jsx, "~> 2.0"}
```

为了将响应内容从字符串转换过来，当Github API返回消息后，我们调用jsx的decode函数：

```elixir
  def handle_response(%{status_code: 200, body: body}) do
    {:ok, :jxs.decode(body)}
  end

  def handle_response(%{status_code: ___, body: body}) do
    {:error, :jxs.decode(body)}
  end
```

我们还必须处理获取数据可能出现的错误，所以回到CLI模块，编写一个在收到成功响应
内容时解码响应体的函数，并返回，否则，从响应体中提取错误信息并显示出来。

```elixir
  def process({user, project, _count}) do
    Issues.GithubIssues.fetch(user, project)
    |> decode_response
  end

  def decode_response({:ok, body}), do: body

  def decode_response({:error, error}) do
    {_, message} = List.keyfind(error, "message", 0)
    IO.puts "Error fetching from Github: #{message}"
    System.halt(2)
  end
```

### 不在hex里的依赖项

mix也可以从其他源加载依赖项，最常见的是github。

例如，在早期hackney没有被加入到hex，添加hackney则需要这样写：

```elixir
def deps do
  [{...},
  {:hackney, github: "benoitc/hackney"}
  ]
end
```

### 应用程序配置

接下来，我们给项目中的每个应用程序编写配置信息。此时我们要配置Issues应用程序，
编写如下代码：

```elixir
config :issues, github_url: "https://api.github.com"
```

每个config 行为给定的应用程序的_environment添加一个或多个键/值对。
如果同一个应用程序有多行，则它们积累起来，后面重复出现的键会覆盖前面的键值。

在我们的代码中我们使用Application.get_env函数返回一个应用程序环境中的值。

```elixir
# github_issues.ex
  @github_url Application.get_env(:issues, :github_url)

  def issues_url(user, project) do
    "#{@github_url}/repos/#{user}/#{project}/issues"
  end
```

## 转换：为数据排序

下一个转换是根据数据的created_at字段对其进行排序。
目前为止，我们会将函数sort/2放在CLI模块里，并时刻留意如果稍后添加相关函数，
是否有机会将它移出这个模块的机会。

```elixir
# cli.ex
  def process({user, project, _count}) do
    Issues.GithubIssues.fetch(user, project)
    |> decode_response
    |> convert_to_list_of_hashdicts
    |> sort_into_ascending_order
  end

  def sort_into_ascending_order(list_of_issues) do
    Enum.sort list_of_issues,
      fn i1, i2 -> i1["created_at"] <= i2["created_at"] end
  end
```

编写CLI的测试：

```elixir
# cli_test.exs
  test "Sort ascending orders the correct way" do
    result = sort_into_ascending_order(fake_created_at_list(["c", "a", "b"]))
    issues = for issue <- result, do: issue["created_at"]
    assert issues == ~w{a b c}
  end

  defp fake_created_at_list(values) do
    data =
      for value <- values,
          do: [{"created_at", value}, {"other_data", "xxx"}]

    convert_to_list_of_hashdicts(data)
  end
```

## 转换：取前n条

使用内建的Enum.take从列表中提取前count条

```elixir
  def process({user, project, count}) do
    Issues.GithubIssues.fetch(user, project)
    |> decode_response
    |> convert_to_list_of_maps
    |> sort_into_ascending_order
    |> Enum.take(count)
  end
```

* 习题：编写出将数据格式化成几列的代码。

```elixir
  def print_table_for_columns(result, columns \\ ["number", "created_at", "title"]) do
    for issue <- result do
      values = fetch_column_value(issue, columns)
      for value <- values, do: IO.puts value
    end
  end

  defp fetch_column_value(issue, columns) do
    for head <- columns do
      issue[head]
    end
  end
```

## 转换：格式化表格

格式化程序：

```elixir
defmodule Issues.TableFormatter do
  import Enum, only: [each: 2, map: 2, map_join: 3, max: 1]

  def print_table_for_columns(rows, headers) do
    data_by_columns = split_into_columns(rows, headers)
    column_widths = widths_of(data_by_columns)
    format = format_for(column_widths)

    [head| tail] = headers
    puts_one_line_in_columns(["#"| tail], format)
    IO.puts(separator(column_widths))
    puts_in_columns(data_by_columns, format)
  end

  def split_into_columns(rows, headers) do
    for header <- headers do
      for row <- rows, do: printable(row[header])
    end
  end

  def printable(str) when is_binary(str), do: str
  def printable(str), do: to_string(str)

  def widths_of(columns) do
    for column <- columns, do: column |> map(&String.length/1) |> max
  end

  def format_for(column_widths) do
    map_join(column_widths, " | ", fn width -> "~-#{width}s" end) <> "~n"
  end

  def separator(column_widths) do
    map_join(column_widths, "-+-", fn width -> List.duplicate("-", width) end)
  end

  def puts_in_columns(data_by_columns, format) do
    data_by_columns
    |> List.zip()
    |> map(&Tuple.to_list/1)
    |> each(&puts_one_line_in_columns(&1, format))
  end

  def puts_one_line_in_columns(fields, format) do
    :io.format(format, fields)
  end
end
```

其中，:io.format是Erlang函数库里用处理格式化标准输出的函数，与:io.fwrite效果等同，
可以试试这段代码：

```elixir
:io.fwrite("|~10.5c|~-10.5c|~5c|~n", [?b, ?c, ?a])
:io.fwrite("|~10.5s|~-10.5s|~5s|~n", ["b", "c", "a"])
```

更多的输出格式请查阅文档。

现在我们已经可以用CLI模块的run函数运行一下完整的过程：

```elixir
Issues.CLI.run(["SasukeBo", "study"])
```
* 由于从guthub API获取的数据包含了中文，:io.fwrite的format中需要增加Unicode转换模式。

由`"~-#{width}s"`改写为`"~-#{width}ts"`


## 任务：创建命令行可执行程序

尽管我们可以通过mix调用run，但是对于其他用户来说是不友好的，所以我们需要创建可以在命令行
运行的程序。

mix可以将我们的的代码和它的依赖项打包到一个单独的文件里。这个文件可以在任何基于Unix的平台上运行。
这要使用Erlang的escript工具。

escript运行程序的时候，它会查找mix.exs文件来获取escript选项。这应该会返回一个escript
配置信息的关键字列表。这些东西最重要的是main_module:，它必须被设定为包含main函数的那个模块的名称。
它将命令行参数以字符列表的列表传递给这个主函数。

因为这似乎会设计命令行，所以我们会将main函数放在Issues.CLI中。以下是mix.exs更新的内容：

```elixir
  def project do
    [
      app: :issues,
      version: "0.1.0",
      elixir: "~> 1.6",
      escript: escript_config,
      start_permanent: Mix.env() == :prod,
      deps: deps()
    ]
  end

  defp escript_config do
    [main_module: Issues.CLI]
  end
```

现在我们向CLI模块添加main函数，实际上我们只需要更改已有的run函数的名字。

```elixir
  def main(argv) do
    argv
    |> parse_args
    |> process
  end
```

然后我们用mix来打包程序：

```
$ mix escript.build
```

执行成功后，项目目录下会多出一个可执行文件`issues`

```
$ ./issues --help
$ ./issues "Sasuke" "study" 3
```
执行上面的命令就会标准输出issue信息列表。

## 任务：添加日志

想象一个庞大的Elixir应用程序可能有许多进程跨越多个节点运行。
在运行过程中，你实在需要一种标准的方法来跟踪重要的事件。
使用Elixir日志器。

默认的mix.exs会为应用程序启动日志器。

日志器支持4个级别的消息，按严重程度递增的顺序是：
debug、info、warn、error。

可以设定在编译时决定要包含的最低日志级别。在config/config.exs中设定。

```elixir
config :logger, compile_time_purge_level: :info
```

配置完就可以添加日志了。
基本的日志函数有Logger.debug、.info、.warn、.error。每个函数接受一个字符串
或一个没有参数的函数作为参数。

```elixir
Logger.debug "Order total #{total(order)}"
Logger.debug fn -> "Order total #{total(order)}" end
```
简述一下上面两行代码的区别：

由于我们已经在config.exs中设置了最低日志级别为info，也即忽略了debug级别，
当执行第一条代码是，程序不会因为日志级别而停止调用total函数来得到结果并插入字符串。

而以函数作为debug的参数时，函数仅在需要该级别日志时才会被调用。大大节约了系统资源。

下面是添加日志后的`Issues.GithubIssues`模块：

```elixir
defmodule Issues.GithubIssues do
  require Logger

  @user_agent [{"User-agent", "Elixir dave@pragprog.com"}]

  def fetch(user, project) do
    Logger.info("Fetching user #{user}'s project #{project}")

    issues_url(user, project)
    |> HTTPoison.get!(@user_agent)
    |> handle_response
  end

  # def issues_url(user, project) do
  #  "https://api.github.com/repos/#{user}/#{project}/issues"
  # end

  # 使用模块属性在编译的时候获取所需的值
  @github_url Application.get_env(:issues, :github_url)

  def issues_url(user, project) do
    "#{@github_url}/repos/#{user}/#{project}/issues"
  end

  def handle_response(%{status_code: 200, body: body}) do
    Logger.info("Successful response")
    Logger.debug(fn -> inspect(body) end)
    {:ok, :jsx.decode(body)}
  end

  def handle_response(%{status_code: status, body: body}) do
    Logger.error("Error #{status} returned")
    {:error, :jsx.decode(body)}
  end
end
```

可以再iex中运行新的代码，就会打印出日志内容，由于配置日志级别，不会显示debug日志。

## 任务：测试代码文档

* Tips:[了解更多Elixir文档规范请点击这里](Standard_Develop_Code_Elixir_Format.md)

给函数写文档，可以把用到这个函数的例子也写进来。

比如TableFormatter模块包含了很多函数，我们可以为它编写文档。

添加文档后的TableFormatter代码就不贴出来了，如果你在github或gitlab查看这个markdown
文件，[可以戳这里](issues/lib/issues/table_formatter.ex)来查看最新的代码。


现在我们来编写一些测试，验证每个iex会话在@doc字符串中的返回值。
创建一个新的测试文件`test/doc_test.exs`:

```elixir
defmodule DocTest do
  use ExUnit.Case
  doctest Issues.TableFormatter
end
```
现在来运行`$ mix test test/doc_test.exs`

`doctest`会自动检测你编写的文档中的`iex>`后的字符串执行结果与下面的输出是否匹配。


## 任务：创建项目文档

关于使用ExDoc来自动生成完整的项目文档系统，在[Elixir文档规范和mix format](Standard_Develop_Code_Elixir_Format.md)
中有部分记录，下面只补充之前学习没涉及的点。

在mix.exs里，可以添加项目名称（如果你的项目在GitHub上）和URL。
后者允许ExDoc在你的源代码里提供可跳转的链接(就是点击方法后面的查看源码链接会跳转到github上你存放的源码页面对应的行)。
这些参数放在project函数里。



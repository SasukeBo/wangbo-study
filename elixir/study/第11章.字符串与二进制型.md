# 字符串与二进制型

## 字符串字面量

Elixir有两种字符串，用单引号和用双引号括起来的。它们最主要的区别是内部表示形式。
但它们也有很多共同点。
* 字符串支持UTF-8编码的字符。
* 可以包含转义字符。
* 允许在Elixir表达式中通过语法#{...}来插值。
* 可以使用反斜杠对有特殊意义的字符进行转义。
* 支持heredoc。


### Heredoc

对多行字符串使用IO.write。IO.puts总是会在字符串结尾加上换行符。

```elixir
IO.write "
    my
    string
    "
IO.write """
  my
  string
  """
```
heredoc 被广泛用于给函数和模块添加文档。

### 魔术符

魔术符以波浪符开头，后接一个大写或小写字母，然后是一些用分隔符限定的内容，可能还有一些选项。

分隔符可以是<...>、{...}、[...]、(...)、|...|、/.../、"..."、'...'
其中的字母决定了魔术符的类型，例如：

```
~C 字符列表，不支持转义或插值
~c 字符列表，支持类似单引号字符串的转义和插值
~R 正则表达式，不支持转义和插值
~r 正则表达式，支持转义和插值
~S 字符串，不支持转义和插值
~s 字符串，类似双引号字符串，支持转义和插值
~W 以空格分割的单词列表，不支持转义或插值
~w 以空格分割的单词列表，支持转义或插值
```

下面是一些例子：

```elixir
~C[1\n2#{1 + 2}]
~c"1\n2#{1 + 2}"
~S[1\n2#{1 + 2}]
~s/1\n2#{1 + 2}/
~W[the c#{'a'}t sat on the mat]
~w[the c#{'a'}t sat on the mat]
```

## “字符串”这个名称

Elixir的约定是，我们只会称双引号字符串时为字符串，单引号的形式是字符列表。
两者区别很大，用于字符串的库也只适合用于双引号形式。

## 单引号字符串——字符编码列表

单引号字符串会被表示为整数值列表，每个值对应字符串中的一个编码点。

```elixir
str = 'wombat'
is_list str
length str
Enum.reverse str
```

令人疑惑的是，iex将其显示为字符串，这是由于当列表中每个数字都是可打印字符时，
iex会将这个整数列表以字符串的形式打印出来。

你可以以多种方式来查看内部表示：

```elixir
:io.format "~w~n", [str]
List.to_tuple str
str ++ [0]
```

格式串中的~w强制将str以Erlang类型，底层的整数列表输出。~n为换行。

因为字符列表也是列表，常规的模式匹配和List函数都适用。

```elixir
'pole' ++ 'vault'
'pole' -- 'vault'
List.zip['abc', '123']
[head | tail] = 'cat'
```

下面这个简单的模块用于解析字符列表表示法，该表示法用于表示任意的有符号十进制数字。
将字符列表转换为整数。

```elixir
# codes/parse.exs
defmodule Parse do

  def number([ ?- | tail ]), do: _number_digits(tail, 0) * -1
  def number([ ?+ | tail ]), do: _number_digits(tail, 0)
  def number(str),           do: _number_digits(tail, 0)

  defp _number_digits([], value), do: value
  defp _number_digits([ digit | tail ], value)
  when digit in '0123456789' do
    _number_digits(tail, value * 10 + digit - ?0)
  end

  defp _number_digits([ non_digit | _ ], _) do
    raise "Invalid digit '#{[non_digit]}'"
  end
end
```

```elixir
c "parse.exs"
Parse.number '123'
Parse.number '-123'
Parse.number '+123'
Parse.number '+9'
Parse.number '+a'
```

* 习题：StringsAndBinaries-1

```elixir
# codes/StringsAndBinaries.exs
defmodule MyStrings do
  def is_ASCII([head | tail]) do
    if head > ?\s - 1 && head < ?~ + 1 do
      is_ASCII(tail)
    else
      false
    end
  end

  def is_ASCII([]), do: true
end
```

* 习题：StringsAndBinaries-2.exs

```elixir
# codes/StringsAndBinaries.exs
defmodule MyStrings do
  def anagram?(word1) do
    Enum.reverse(word1) == word1
  end
end
```

* 习题：StringsAndBinaries-3.exs

```elixir
# codes/StringsAndBinaries.exs
defmodule MyStrings do
  def calculate(expression) when is_list expression do
    analyze(expression, 0)
  end

  # when里面不能使用&&，||，！，可以使用 and、or、not
  defp analyze([head | tail], value) when (head >= ?0 and head <= ?9) do
    analyze(tail, value * 10 + (head - ?0))
  end
  defp analyze([head | tail], value) when head == ?+ do
    value + analyze(tail, 0)
  end
  defp analyze([head | tail], value) when head == ?- do
    value - analyze(tail, 0)
  end
  defp analyze([head | tail], value) when head == ?* do
    value * analyze(tail, 0)
  end
  defp analyze([head | tail], value) when head == ?/ do
    value / analyze(tail, 0)
  end
  defp analyze([head | tail], value) when head == ?\s do
    analyze(tail, value)
  end
  defp analyze([], value), do: value
end
```

## 二进制型

二进制表示位的序列,
二进制型字面值是这样子的：<< term, ... >>。

最简单的项就是0至255的数字。数字以连续字节存储在二进制型中。

```elixir
b = << 1, 2, 3 >>
byte_size b
bit_size b
```

你可以通过指定修饰符来设定任意项的大小（以二进制位为单位）。
这在处理二进制格式的数据（例如媒体文件和网络数据包）时非常有用。

```elixir
# b = 00001001
b = <<1::size(2), 1::size(3)>>
byte_size b
bit_size b
```

你可以在二进制型中存储整数、浮点数和其他二进制数据。

```elixir
int = << 1 >>
float = << 2.5 :: float >>
mix = <<int :: binary, float :: binary>>
```

IEEE 754标准的浮点数包含符号位、指数、底数，可以提取这些字段

```elixir
<<sign::size(1), exp::size(11), mantissa::size(52)>> = <<3.1415926::float>>
(1 + mantissa / :math.pow(2, 52)) * :math.pow(2, exp - 1023)
```

## 双引号字符串是二进制型

双引号字符串的内容是以UTF-8编码存储的连续字节序列。

```elixir
dqs = "∂x/∂y"
String.length dqs
byte_size dqs
String.at(dqs, 0)
String.codepoints(dqs)
String.split(dqs, "/")
```

因为不是使用列表，所以在编码中要学习使用二进制型的语法。

### 字符串与Elixir库

String 模块定义了很多处理双引号字符串的函数。

```elixir
# at(str, offset)
# 返回给定位移的语义符（从0开始）。负数位移从字符串的末尾数起。
String.at("∂og", 0)
String.at("∂og", -1)

# capitalize(str)
# 将str转换成小写，然后将首字符转换成大写
String.capitalize "aBcDeFg"

# codepoints(str)
# 返回str中的编码点。
String.codepoints("sasuke")

# downcase(str)
# 将str转换为小写
String.downcase "SASUKE"

# duplicate(str, n)
# 返回包含n个str的拷贝的字符串
String.duplicate "Ho! ", 3

# ends_with?(str, suffix | [suffixes])
# 如果str以给定后缀的其中一个为结尾，就返回true。
String.ends_with? "String", ["elix", "stri", "ring"]

# first(str)
# 返回str中的第一个语义符。
String.first "∂og"

# graphemes(str)
# 返回字符串中的语义符。这与codepoints函数不同，codepoints将组合字符单独列出来。

# last(str)
# 返回str中的最后一个语义符
String.last "∂og"

# length(str)
# 返回str中语义符的数目。
String.length "∂x/∂y"

# ljust(str, new_length, padding \\ " ")
# 返回一个新字符串，长度至少为new_length个字符，将str左对齐，并用padding填充空白的位置
String.ljust("cat", 5)

# lstrip(str)
# 删除str开头的空字符
String.lstrip "\t\f   Hello\t\n" # 该方法已经被弃用
String.trim_leading "\t\f   Hello\t\n"


# lstrip(str, character)
# 删除str开头出现的character（一个整数编码点）
String.lstrip "!!!SALE!!!", ?! # 该方法已经弃用
String.trim_leading "!!!SALE!!!", "!"

# next_codepoint(str)
# 将str分割成前导编码点和剩下的部分，如果str为空则返回nil。这个函数可用于填写迭代器。
# codes/nextcodepoint.ex

defmodule MyString do
  def each(str, func), do: _each(String.next_codepoint(str), func)

  defp _each({codepoint, rest}, func) do
    func.(codepoint)
    _each(String.next_codepoint(rest), func)
  end

  defp _each(nil, _), do: []
end

# next_grapheme(str)
# 和next_codepoint一样，但返回的是语义符

# printable?(str)
# 如果str只包含可打印字符，则返回true。
String.printable? "Jose"
String.printable? "\x{0000} a null"
```

更多请查阅Elixir文档

* 习题：StringsAndBinaries-5

    编写一个函数，接受一个双引号字符串列表作为参数，并将每个字符串打印在单独一行
    上，以最长字符串的宽度来居中对齐。


```elixir
# codes/StringsAndBinaries.exs

defmodule MyStrings do
  import String
  def center(string_list) do
    count_size_max(string_list, 0) |> print_str(string_list)
  end

  defp count_size_max([head | tail], max_size) do
    if byte_size(head) > max_size do
      count_size_max(tail, byte_size(head))
    else
      count_size_max(tail, max_size)
    end
  end

  # defp count_size_max([], max_size), do: max_size
  defp count_size_max([], max_size) do
    max_size
  end

  defp print_str(max_size, string_list) do
    for str <- string_list do
      lsize = (max_size - byte_size(str)) |> div(2)
      pad_leading(str, String.length(str) + lsize)  |> IO.puts()
    end
  end
end
```

## 二进制型与模式匹配

二进制型的第一法则是“如果有疑问，就给各个字段指定类型”。

可用的类型有binary、bits、bitstring、bytes、float、integer、utf8、utf16和utf32。
你也可以添加限定符：
* size(n): 字段的二进制位数目。
* signed或unsigned: 对于整数字段，它应该被解释成有符号吗？
* 字节序: big、little或native。


连接符用来分隔字段的多个属性：

```elixir
<<length::unsigned-integer-size(12), flags::bitstring-size(4)>> = data
```

### 用二进制型来处理字符串

可以像分割列表一样分割二进制型。指定头部类型并且确保尾部依然是二进制型。

```elixir
# codes/utf-iterate.ex
defmodule Utf8 do
  def each(str, func) when is_binary(str), do: _each(str, func)

  defp _each(<< head :: utf8, tail :: binary >>, func) do
    func.(head)
    _each(tail, func)
  end

  defp _each(<<>>, _func), do: []
end
```

```elixir
Utf8.each "∂og", fn char -> IO.puts char end
```

* 习题：StringsAndBinaries-6

```elixir
defmodule Utf8 do

  def capitalize_sentences(str) do
    list = String.split str, ~r{(\.\s)}, include_captures: true
    str_list = for str <- list , do: String.capitalize(str)
    Enum.reduce(str_list, fn(str, total_str) -> total_str <> str end)
  end
end
```

```elixir
Utf8.capitalize_sentences "thiS IS a line. and I SAID the secOND line. one. tWo. thReE. "
```

* 习题： StringsAndBinaries-7

```elixir
defmodule MyString do
  # 编写一个函数读取和解析文件tax_data，得出的结果可以由ListsAndRecursion-8.exs文件中定义的方法make_total处理。
  def read_tax_data(str) do
    {:ok, file} = File.open(str)
    stream = IO.stream(file, :line)
    list = for str <- stream, do: String.trim_trailing(str, "\n") |> String.split(",")
    return_a_list(list, 0, [], [])
  end

  defp return_a_list([head | tail], i, list_head, good_list) do
    if i == 0 do
      return_a_list(tail, 1, tran_key_map(head), good_list)
    else
      return_a_list(tail, 1, list_head, good_list ++ [Enum.zip(list_head, tran_data_map(head))])
    end
  end

  defp return_a_list([], _, _, good_list), do: good_list

  defp tran_key_map(list) do
    for str <- list, do: String.to_charlist(str) |> List.to_atom
  end

  defp tran_data_map([id, city, net_amount]) do
    [
      String.to_integer(id),
      String.trim_leading(city,":") |> String.to_atom,
      String.to_float(net_amount)
    ]
  end
end
```

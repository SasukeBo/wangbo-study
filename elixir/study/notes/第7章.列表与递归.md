# 列表与递归

## 头部与尾部

头部包括一个值，而尾部本身则是一个列表。

```elixir
[ head | tail ] = [ 1, 2, 3 ]
```

## 使用头部和尾部来处理列表

* 列表的长度为0。
* 列表长度等于列表尾部长度加1。
用Elixir来实现这个功能非常容易：

```elixir
# codes/mylist.exs
defmodule MyList do
  def len([]), do: 0
  # 变量未被用到可以使用_来关闭编译时的警告
  def len([_head|tail]), do: 1 + len(tail)
end
```

Elixir有两种表示字符串的形式。
* 字面量加双引号是在连续内存存储的字符序列。
* 单引号将字符串表示为一个整数代码点的列表。
如果列表中的所有值都可以表示为可打印字符，Elixir就将列表表示为字符串，否则显示为整数列表。
当然后面会讨论如何避开这种行为。

```elixir
[99, 97, 116] # 'cat'
[99, 97, 116, 0] # '0'是不可打印字符
```

## 使用头部和尾部来构造列表

写一个函数接受一个数字列表，并返回一个包含其中每个数字的平方的列表。

```elixir
# codes/mylist.exs
def square([]), do: []
def square([ head | tail ]), do: [ head * head | square(tail) ]
```

写一个类似的函数，将列表中元素都加一。

```elixir
# codes/mylist.exs
def add_1([]), do: []
def add_1([head | tail]), do: [head + 1| add_1(tail)]
```

## 创建映射函数

定义一个函数，接受一个列表和一个函数，返回一个新列表，其元素为元列表中各元素应用于函数之后的结果。

```elixir
# codes/mylist.exs
def map([], _func), do: []
def map([head | tail], func), do: [func.(head) | map(tail, func)]
```

## 在递归过程中跟踪值

在按顺序处理每个元素的时候记住部分元素的总和。我们可以将状态传入函数的参数里。

```elixir
# codes/sum.exs
defmodule MyList do
  def sum([], total), do: total
  def sum([head | tail], total), do: sum(tail, head + total)
end
```

但这样调用时必须传入一个初始的和值，显得笨拙。而Elixir的约定是把它隐藏起来，使模块包含一个只接受一个列表的公开函数，其调用私有函数来完成真正的工作。

```elixir
# codes/sum2.exs
defmodule MyList do
  def sum(list), do: _sum(list, 0)

  #私有方法
  defp _sum([], total), do: total
  defp _sum([head | tail], total), do: _sum(tail, head + total)
end
```
* 我们使用defp来定义私有函数，模块外无法调用这些函数。
* 我们给辅助函数选择与公开函数一样的名称，但以下划线开头。

* 习题：ListAndRecursion-0

```elixir
def sum([]), do: 0
def sum([head | tail]), do: head + sum(tail)
```

### 生成求和函数

编写一个通用的函数来将一个收集规约为一个值

```elixir
# codes/reduce.exs
defmodule MyList do
  def reduce([], value, _) do
    value
  end
  def reduce([head | tail], value, func) do
    reduce(tail, func.(head, value), func)
  end
end
```

* 习题：ListsAndRecursion-1

```elixir
# codes/ListsAndRecursion-1.exs
defmodule MyList do
  def mapsum(list, func), do: _mapsum(list, func, 0)

  defp _mapsum([], _, value), do: value
  defp _mapsum([head | tail], func, value), do: _mapsum(tail, func, value + func.(head))
end
```

* 习题：ListAndRecursion-2

```elixir
# codes/ListsAndRecursion-2.exs
defmodule MyList do
  def max(list), do: hmax(list, 0)

  defp hmax([], value), do: value
  defp hmax([head | tail], value) when head > value, do: hmax(tail, head)
  defp hmax([head | tail], value) when head <= value, do: hmax(tail, value)
end
```

* 习题：ListsAndRecursion-3

```elixir
# codes/ListsAndRecursion-3.exs
defmodule MyList do
  def caesar([], _n), do: []
  def caesar([head | tail], n) do
    [do_caesar(head, n) | caesar(tail, n)]
  end

  defp do_caesar(head, n) do
    ?a + rem head + n - ?a, 26
  end
end
```

## 更复杂的列表模式

不是所有的列表问题都可以简单的通过一次处理一个元素的模式来解决。
连接运算符|，其左边可以有多个值。例如：

```elixir
[1, 2, 3 | [ 4, 5, 6 ]]
```
在模式中也可以这样用，所以可以匹配多个单独的元素作为头部，例如：

```elixir
# codes/swap.exs
defmodule Swapper do
  def swap([]), do: []
  def swap([a, b | tail]), do: [b, a | swap(tail)]
  def swap([_]), do: raise("无法处理奇数个元素的列表")
end
```
### 列表的列表

```elixir
# codes/weather.exs
defmodule WeatherHistory do
  def for_location_27([]), do: []

  def for_location_27([[time, 27, temp, rain] | tail]) do
    [[time, 27, temp, rain] | for_location_27(tail)]
  end

  def for_location_27([_ | tail]), do: for_location_27(tail)
end
```
这是标准的递归至列表为空的模式。
要使第二个子句的模式匹配，列表的头部必须是含有四个元素的列表，而子列表的第二个元素必须是27。
做这样的过滤时，还要处理函数不匹配的情况，就是第三个子句的作用。

接着我们在模块里定义一些简单的测试数据。

```elixir
# codes/testData.exs
defmodule TestData do
  def test_data do
    [
      [1_366_225_622, 26, 15, 0.125],
      [1_366_225_622, 27, 15, 0.45],
      [1_366_225_622, 28, 21, 0.25],
      [1_366_229_222, 26, 19, 0.081],
      [1_366_229_222, 27, 17, 0.468],
      [1_366_229_222, 28, 15, 0.60],
      [1_366_232_822, 26, 22, 0.095],
      [1_366_232_822, 27, 21, 0.05],
      [1_366_232_822, 28, 24, 0.03],
      [1_366_236_422, 26, 17, 0.025]
    ]
  end
end
```
为了更方便的调用函数，我们可以在iex中import模块，这样调用时不需要写模块名。

```elixir
c "testData.exs"
c "weather.exs"
import TestData
import WeatherHistory
for_location_27(test_data)
```

我们的函数制定了一个特定的地区，这样非常限制函数的功能，想要将地区也作为参数传递进来，可以使用模式匹配来实现。

```elixir
# codes/weather2.exs
defmodule WeatherHistory do
  def for_location([], _target_loc), do: []

  def for_location([[time, target_loc, temp, rain] | tail], target_loc) do
    [[time, target_loc, temp, rain] | for_location(tail, target_loc)]
  end

  def for_location([_ | tail], target_loc), do: for_location(tail, target_loc)
end
```

现在第二个函数只有在列表头部提取出来的地区与参数传入的目标地区相同时才会触发。

改进：我们的过滤器不关心头部的其他三个元素，只关心地区编号，我们可以在模式内进行模式匹配。

```elixir
# codes/weather3.exs
defmodule WeatherHistory do
  def for_location([], _target_loc), do: []

  def for_location([head = [_, target_loc, _, _] | tail], target_loc) do
    [head | for_location(tail, target_loc)]
  end
  def for_location([_ | tail], target_loc), do: for_location(tail, target_loc)
end
```

* 习题：ListAndRecursion-4

```elixir
# codes/ListAndRecursion-4.exs
defmodule MyList do
  def span(from, to), do: do_span([to], from, to)

  defp do_span(list, from, to) when to > from do
    do_span([to - 1 | list], from, to - 1 )
  end

  defp do_span(list, from, to) when to == from, do: list
end
```

## List 模块实战

List 模块提供了一组操作列表的函数

```elixir
# 链接列表
[1, 2, 3] ++ [4, 5, 6]
# 将列表一维化
List.flatten([[[1], 2], [[[3]]]])
# 折叠
List.foldl([1, 2, 3], "", &("#{&1}(#{&2})"))
List.foldr([1, 2, 3], "", &("#{&1}(#{&2})"))
# 合并与拆分列表
l = List.zip([[1, 2, 3], [:a, :b, :c], ["cat", "dog"]])
# 在列表里访问元组
kw = [{:name, "Dave"}, {:likes, "Programming"}, {:where, "Dallas", "TX"}]
List.keyfind kw, "Dallas", 1
List.keyfind kw, "TX", 2
List.keyfind kw, "TX", 1
List.keyfind kw, "TX", 1, "No city called TX"
kw = List.keydelete kw, "TX", 2
kw = List.keyreplace kw, :name, 0, {:first_name, "Dave"}
```

# 匿名函数
Elixir 是函数式语言，函数是它的一种基本类型。
匿名函数使用 fn 关键字创建。
```elixir
fn
  parameter-list -> body
  parameter-list -> body
end
```
最简单的情况是一个函数拥有一个参数列表和一个函数体，二者通过 -> 隔开。
例如：
```elixir
sum = fn(a, b) -> a + b end
sum.(1, 2)
```
上面代码将这个匿名函数存储在sum变量里。
点符号表示函数调用，括号中是被传递的参数。
如果函数不接收参数，还是要使用圆括号调用它：
```elixir
greet = fn -> IO.puts "Hello" end
greet.()
```
不过可以在函数定义中省略圆括号。

## 函数和模式匹配

函数调用时Elixir尝试将这些实参和形参进行匹配。
这意味着调用函数时，可以进行很复杂的模式匹配。例如下面的函数调换元组中两个元素的位置：
```elixir
swap = fn {a, b} -> {b, a} end
swap.({"sasuke", "hello"})
```
## 一个函数，多个函数体

在单个函数定义中，可以定义不同的实现，这取决于传入参数类型和内容。
你不能根据参数数目进行选择，函数定义中的每个字句必须拥有相同数目的参数。
```elixir
handle_open = fn
  {:ok, file} -> "Read data: #{IO.read(file, :line)}"
  {_,error} -> "Error: #{:file.format_error(error)}"
end
handle_open.(File.open("hello.exs"))
```
在字符串中,#{...}内容会被求值，并且求值结果会被转换为字符串。
```elixir
function_2 = fn
  {0, 0, _} -> "FizzBuzz"
  {0, _, _} -> "Fizz"
  {_, 0, _} -> "Buzz"
  {_, _, c} -> c
end

IO.puts function_2.({0, 0, 1})
IO.puts function_2.({0, 1, 1})
IO.puts function_2.({1, 1, 1})
IO.puts function_2.({1, 0, 1})
```
```elixir
function_3 = fn(n) -> function_2.({rem(n, 3), rem(n, 5), n}) end

IO.puts function_3.(10)
IO.puts function_3.(11)
IO.puts function_3.(12)
IO.puts function_3.(13)
IO.puts function_3.(14)
IO.puts function_3.(15)
IO.puts function_3.(16)
```
## 能返回函数的函数

```elixir
fun1 = fn -> fn -> "Hello" end end
fun1.()
fun1.().()

other = fun1.()
other.()end
```
### 记住原始环境的函数

```elixir
greeter = fn name -> (fn -> "Hello #{name}" end) end
dave_greeter = greeter.("Dave")
dave_greeter.()
```

### 参数化函数

```elixir
add_n = fn n -> (fn other -> n + other end) end
add_two = add_n.(2)
add_five = add_n.(5)
add_two.(3)
add_five.(7)
```
* 习题：Functions-4

```elixir
prefix = fn call -> (fn name -> call <> " " <> name end) end
mrs = prefix.("Mrs")
mrs.("Smith")
```
## 将函数作为参数来传递

函数就是值，所以我们可以将它们传递给其他参数。
```elixir
times_2 = fn n -> n * 2 end
apply = fn (fun, value) -> fun.(value) end
apply.(times_2, 6 )
```
在Elixir代码中很多地方都是通过这个特性来传递函数的。
例如内建的Enum模块有个名为map的函数。
它接受两个参数：一个收集，和一个函数。
它返回一个列表，将收集中的每个元素应用于那个函数之后所得的结果。
```elixir
list = [1, 3, 5, 7, 9]
Enum.map list, fn elem -> elem * 2 end
Enum.map list, fn elem -> elem * elem end
Enum.map list, fn elem -> elem > 6 end
```
### &运算符

可以用&来替代fn写法
```elixir
add_one = &(&1 + 1) # 等同于add_one = fn n -> n + 1 end
add_one.(0)
(square = &(&1 * &1)).(2)
speak = &(IO.puts(&1))
speak.("hello")
```
要保证这种方式能正确工作，参数必须以正确的顺序排列
```elixir
rnd = &(Float.round(&1, &2))
rnd = &(Float.round(&2, &1))
```
字面量的列表和元组也可以被转换成函数
```elixir
divrem = &{ div(&1, &2), rem(&1, &2) }
divrem.(10,2)
```

当输入&(IO.puts(&1))到iex时，它显示的结果是&IO.puts/1。在这种情况下，puts是IO模块里的函数，它接受一个参数。
Elixir对于这种方式的命名方法是IO.puts/1。如果我们在这个名字前放个&，就是将它封装到一个函数里。
```elixir
l = &length/1
l.([1, 3, 5, 7])
len = &Enum.count/1
len.([1, 2, 3, 4])
m = &Kernel.min/2 # 这是 Erlang 函数的一个别名
m.(99, 88)
```
* 习题：Function-5

```elixir
list = [1, 2, 3, 4]
add_two = &Enum.map/2
add_two.(list, &(&1 + 2))
inspect = &Enum.each/2
inspect.(list, &(IO.inspect &1))
```

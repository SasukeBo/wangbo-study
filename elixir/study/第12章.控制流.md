# 控制流

在Elixir中，我们编写许多小函数，并联合使用哨兵子句和参数的模式匹配，替代了大多数其他语言中所见的控制流。

Elixir代码倾向于声明式而非命令式。

## if与unless

```elixir
if 1 == 1, do: "true part", else: "false part"
```
unless是类似的，只不过和if相反。

## cond

cond宏允许列出一系列条件，每个条件对应一段代码。第一个满足的条件所对应的代码会被执行。

一段示例代码：

```elixir
# codes/fizzbuzz.ex
defmodule fizzbuzz do

  def upto(n) when n > 0, do: _upto(1, n, [])

  def _upto(_current, 0, result), do: Enum.reverse result

  def _upto(current, left, result) do
    next_answer =
      cond do
        rem(current, 3) == 0 and rem(current, 5) == 0 ->
          "FizzBuzz"
        rem(current, 3) == 0 ->
          "Fizz"
        rem(current, 5) == 0 ->
          "Buzz"
        true ->
          current
      end
    _upto(current + 1, left - 1, [next_answer | result])
  end
end
```

换一种思路

```elixir
defmodule FizzBuzz do
  def upto(n) when n > 0 do
    1..n |> Enum.map(&fizzbuzz/1)
  end

  defp fizzbuzz(n) do
    cond do
      rem(n, 3) == 0 and rem(n, 5) == 0 ->
        "FizzBuzz"
      rem(n, 3) == 0 ->
        "Fizz"
      rem(n, 5) == 0 ->
        "Buzz"
      true ->
        n
    end
  end
end
```

本节的目的是介绍cond如何工作，但是你常常会发现最好不要使用cond，而是选择使用函数
调用的模式匹配：

```elixir
defmodule FizzBuzz do
  def upto(n) when n > 0 do
    1..n |> Enum.map(&fizzbuzz/1)
  end

  defp fizzbuzz(n) when rem(n, 3) == 0 and rem(n, 5) == 0, do: "FizzBuzz"
  defp fizzbuzz(n) when rem(n, 3) == 0, do: "Fizz"
  defp fizzbuzz(n) when rem(n, 5) == 0, do: "Buzz"
  defp fizzbuzz(n), do: n
end
```

## case

case测试一组模式，执行第一个匹配成功的模式所对应的代码，并返回代码的值。模式可以包含哨兵子句。

```elixir
# codes/case.ex
case File.open("./codes/case.ex") do
  {:ok, file} ->
    IO.puts "First line: #{IO.read(file, :line)}"
  {:error, reason} ->
    IO.puts "Failed to open file: #{reason}"
end
```

我们可以使用嵌套模式匹配的全部功能：

```elixir
defmodule Users do
  dave = %{ name: "Dave", state: "TX", likes: "Programming" }

  case dave do
    %{state: some_state} = person ->
      IO.puts "#{person.name} lives in #{some_state}"

    _ ->
      IO.puts "No matches"
  end
end
```

当匹配函数时，我们介绍过如何引入哨兵子句来提炼模式，case也一样。

```elixir
defmodule Bouncer do

  dave = %{name: "Dave", age: 27}

  case dave do
    %{age: age} = person when is_number(age) and age >= 21 ->
      IO.puts "You are cleared to enter the Foo Bar, #{person.name}"

    _ ->
      IO.puts "Sorry, no admission"
  end
end
```

## 抛出异常

Elixir中的异常不属于控制流。

raise函数抛出异常，最简单的形式，传递一个str给它就能产生一个RuntimeError类型的异常。

```elixir
raise "Giving Up"
```

也可以传入异常类型，以及其他可选属性，所有异常实现至少包含message属性。

```elixir
raise RuntimeError, message: "override message"
```

## 包含异常的设计

如果知道文件可能打开不成功，并希望进行处理，代码可以这样写：

```elixir
case File.open(file_name) do
  {:ok, file} ->
    Enum.each(IO.stream(file, :line), &IO.puts/1)
  {:error, message} ->
  IO.puts :stderr, "Couldn't open #{file_name}: #{message}"
end
```

* 习题：ControlFlow-1

    使用case重写FizzBuzz的例子

```elixir
defmodule FizzBuzz do

  def upto(n) when n > 0, do: _upto(1, n, [])

  def _upto(_current, 0, result), do: Enum.reverse result

  def _upto(current, left, result) do
    next_answer =
      case rem(current, 3) do
        0 ->
          case rem(current, 5) do
            0 -> "FizzBuzz"
            _ -> "Fizz"
          end
        _ ->
          case rem(current, 5) do
            0 -> "Buzz"
            _ -> current
          end
      end
    _upto(current + 1, left - 1, [next_answer | result])
  end
end
```

* 习题：ControlFlow-3

    编写一个ok!函数，接受任意参数。如果参数是元组{:ok, data}, 返回data，否则抛出包含参数信息的异常。

```elixir
  def ok!(param) do
    case param do
      {:ok, data} ->
        data
      {:error, message} ->
        raise "The file couldn't be open: #{message}"
      _ ->
        raise "The \"#{param}\" couldn't be processed!"
    end
  end
```

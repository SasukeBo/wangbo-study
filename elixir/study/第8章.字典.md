# 字典：散列表、散列字典、关键字列表、集合与结构体

字典是将关键值关联起来的数据结构。
接下来将学习如何使用它们进行模式匹配。

## 如何在散列表、散列字典和关键字列表之间做选择

* 将多个条目对应同一个键： 使用Keyword模块
* 保证元素的次序：使用Keyword模块
* 要对内容进行模式匹配：使用散列表
* 需要存储数百个条目：使用HashDict

## 字典

散列表和散列字典都实现了Dict的行为。Keyword模块也基本实现了，不同之处在于它支持重复键。

```elixir
defmodule Sum do
  def values(dict) do
    dict |> Dict.values() |> Enum.sum()
  end
end

# 对散列字典中的值求和
# Enum.into 可以方便地把一种类型的收集映射成另一种。
hd = [one: 1, two: 2, three: 3] |> Enum.into(HashDict.new())
IO.puts(Sum.values(hd))

# 对散列表中的值求和。
map = %{four: 4, five: 5, six: 6}
IO.puts(Sum.values(map))
```
*HashDict.new/0 is deprecated, use maps and the Map module instead*

*Dict.values/1 is deprecated, use the Map module for working with maps or the
Keyword module for working with keyword lists*

Dict在最新版本的Elixir已经被弃用了，现在是使用Map模块来完成它的工作。

```elixir
kw_list = [name: "Dave", likes: "Programming", where: "Dallas"]
hashdict = Enum.into kw_list, HashDict.new
map = Enum.into kw_list, Map.new
kw_list[:name]
hashdict[:likes]
map[:where]
hashdict = Dict.drop(hashdict, [:where, :likes])
hashdict = Dict.put(hashdict, :also_likes, "Ruby")
combo = Dict.merge(map, hashdict)

kw_list = [name: "Dave", likes: "Programming", likes: "Elixir"]
kw_list[:likes]
Dict.get(kw_list, :likes)
# 关键字列表允许出现重复的值，但是要使用Keyword模块才能访问它们。
Keyword.get_values(kw_list, :likes)
```

## 模式匹配和更新散列表

```elixir
person = %{name: "Dave", height: 1.88}
%{name: a_name} = person
a_name

%{name: _, height: _} = person
%{name: "Dave"} = person

# 这个散列表没有weight，所以下面的匹配将会失败
%{name: _, weight: _ } = person
```
第一个模式匹配通过提取键值:name关联的值来实现的。这种方法很常用。

```elixir
# codes/query.exs
defmodule Maps do
  def load_people do
    _people = [
      %{name: "Grumpy", height: 1.24},
      %{name: "Dave", height: 1.88},
      %{name: "Dopey", height: 1.32},
      %{name: "Shaquille", height: 2.16},
      %{name: "Sneezy", height: 1.28}
    ]
  end
end

```

```elixir
for person = %{height: height} <- Maps.load_people,
    height > 1.5,
    do: IO.inspect(person)
```

```elixir
# codes/book_room.exs
defmodule HotelRoom do
  def book(%{name: name, height: height})
  when height > 1.9 do
    IO.puts "Need extra long bed for #{name}"
  end

  def book(%{name: name, height: height})
  when height < 1.3 do
    IO.puts "Need low shower controls for #{name}"
  end

  def book(person) do
    IO.puts "Need regular bed for #{person.name}"
  end
end
```

```elixir
Maps.load_people |> Enum.each(&HotelRoom.book/1)
```

### 模式匹配不能绑定键

散列表在模式匹配时不允许将一个值绑定到键。

*因此你不能这样写：*

```elixir
%{item => :ok} = %{1 => :ok, 2 => :error}
```

*但你可以这样写*

```elixir
%{2 => state} = %{1 => :ok, 2 => :error}
```
也就是你不能获取散列表的键。只能获取值。

## 更新散列表

对于散列表，无需遍历整个结构就能够新增键值对和更新已存在的项。
更新散列表的最简单的方法是使用如下语法：

```elixir
new_map = %{old_map | key => value, ...}
```

这会创建一个新的散列表，它是旧散列表的拷贝，但是管道运算符右边与键对应的值会被
更新：

```elixir
m = %{a: 1, b: 2, c: 3}
m1 = %{m | b: "two", c: "three"}
m2 = %{m1 | a: "one"}
```
增加新的键值则需要借助Map.put_new/3函数
```elixir
m4 = Map.put_new(m, :d, 4)
```

### 散列表与结构体

如果要创建一个带类型的散列表，具有固定字段集合以及与之对应默认值的散列表，并且
能够像匹配内容一样进行模式匹配，就要使用结构体。

结构体就是模块。
在模块内部，使用defstrust宏来定义散列表的性质。

```elixir
# codes/defstruct.exs
defmodule Subscriber do
  defstruct name: "", paid: false, over_18: true
end
```
```elixir
s1 = %Subscriber{}
s2 = %Subscriber{name: "Dave"}
s3 = %Subscriber{name: "Mary", paid: true}
```
创建结构体和创建散列表语法一样，在%和{之间加上模块名即可。
可以通过点标记和模式匹配访问结构的字段。

```elixir
s3.name
%Subscriber{name: a_name} = s3
a_name
```

使用如下方式更新结构体：

```elixir
s4 = %Subscriber{s3 | name: "Marie"}
```

如何将结构体置于模块中？

```elixir
# codes/defstruct1.exs
defmodule Attendee do
  defstruct name: "", paid: false, over_18: true

  def may_attend_after_party(attendee = %Attendee{}) do
    attendee.paid && attendee.over_18
  end

  def print_vip_badge(%Attendee{name: name}) when name != "" do
    IO.puts "Very cheap badge for #{name}"
  end

  def print_vip_badge(%Attendee{}) do
    raise "missing name for badge"
  end
end
```
```elixir
a1 = %Attendee{name: "Dave", over_18: true}
Attendee.may_attend_after_party(a1)
a2 = %Attendee{a1 | paid: true}
Attendee.may_attend_after_party(a2)
Attendee.print_vip_badge(a2)
a3 = %Attendee{}
Attendee.print_vip_badge(a3)
```

### 嵌套字典结构

前面的字典类型让键和值相关联，但是这些值本身也可以是字典类型。

```elixir
# codes/nested.exs
defmodule Customer do
  defstruct name: "", company: ""
end

defmodule BugReport do
  defstruct owner: %{}, details: "", severity: 1
end
```

我们可以通过常规的点符号来访问嵌套字段

```elixir
report = %BugReport{
          owner: %Customer{name: "Dave", company: "Pragmatic"},
          details: "broken"
          }
report.owner.company
```

如果要修改嵌套字段的值则需要这样：

```elixir
report = %BugReport{report | owner:
                    %Customer{report.owner | company: "PragProg"}}
```

Elixirt提供了更好的方法解决这个问题：

```elixir
put_in(report.owner.company, "PragProg")
```

update_in函数可以在结构体的某个值上执行一个函数

```elixir
update_in(report.owner.name, &("Mr. " <> &1))
```

另外两个嵌套访问函数时get_in和get_and_update_in。

### 嵌套访问器和非结构体

嵌套访问函数使用Access协议去拆解和重组数据结构，所以，使用散列表和关键字列表可以使用原子作为键。

```elixir
report = %{owner: %{name: "Dave", company: "Pragmatic"}, severity: 1}
put_in(report[:owner][:company], "PragProg")
update_in(report[:owner][:name], &("Mr. " <> &1))
```

### 动态（运行时）嵌套访问器

迄今为止我们见到的嵌套访问器都是宏，它们工作在编译期。

```elixir
# codes/dynamic_nested.exs
nested = %{
  buttercup: %{
    actor: %{
      first: "Robin",
      last: "Wright"
    },
    role: "princess"
  },
  westley: %{
    actor: %{
      first: "Carey",
      last: "Ewes"
    },
    role: "farm boy"
  }
}

IO.inspect(get_in(nested, [:buttercup]))
IO.inspect(get_in(nested, [:buttercup, :actor]))
IO.inspect(get_in(nested, [:buttercup, :actor, :first]))
IO.inspect(put_in(nested, [:westley, :actor, :last], "Elwes"))
```

get_in和get_and_update_in的动态版本还支持一个很酷的特性，如果将函数作为键传入，这个函数会被调用来返回相关的值。

```elixir
# codes/get_in_func.exs
authors = [
  %{name: "Jos\u0065\u0301", language: "Elixir"},
  %{name: "Matz", language: "Ruby"},
  %{name: "Larry", language: "Perl"}
]

languages_with_an_r = fn(:get, collection, next_fn) ->
  for row <- collection do
    if String.contains?(row.language, r"") do
      next_fn.(row)
    end
  end
end

IO.inspect get_in(authors, [languages_with_an_r, :name])
```

## 集合

MapSet 取代了老版本的HashSet

```elixir
set1 = Enum.into 1..5, MapSet.new
MapSet.member? set1, 3
set2 = Enum.into 3..8, MapSet.new
MapSet.union set1, set2
MapSet.difference set2, set1
MapSet.intersection set1, set2
```

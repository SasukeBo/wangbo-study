# 处理收集——Enum 与 Stream

Enum模块是收集的骨干部分。
Stream模块让遍历收集操作延迟执行。

## Enum——处理收集

Enum模块用于迭代、过滤、组合、分割和其他收集操作。
下面展示一些常见用法：

```elixir
# 将任意收集转换成列表
list = Enum.to_list 1..5

# 将多个收集连接成一个收集
Enum.concat([1, 2, 3], [4, 5, 6])
Enum.concat([1, 2, 3], 'abc')

# 创建收集，其元素是收集元素应用于某些函数后的结果
Enum.map(list, &(&1 * 10))
Enum.map(list, &String.duplicate("*", &1))

# 根据位置和条件选择元素
Enum.at(10..20, 3)
Enum.at(10..20, 20)
Enum.at(10..20, 20, :no_one_here)
Enum.filter(list, &(&1 > 2))
Enum.filter(list, &Integer.is_even/1)
Enum.reject(list, &Integer.is_even/1)

# 排序和比较元素
string_list = ["there", "was", "a", "crooked", "man"]
Enum.sort string_list
Enum.sort string_list, &(String.length(&1) <= String.length(&2))
Enum.max string_list
Enum.max_by string_list, &String.length/1

# 分割收集
Enum.take(list, 3)
Enum.take_every list, 2 # 提取从首元素开始每隔2隔元素的元素
Enum.take_while list, &(&1 < 4)
Enum.split list, 3
Enum.split_while list, &(&1 < 4)

# 将收集中所有的元素连接成一个字符串
Enum.join list
Enum.join list, ", "

# 断言操作
Enum.all? list, &(&1 < 4)
Enum.any? list, &(&1 < 4)
Enum.member? list, 4
Enum.empty? list

# 将两个收集合并为一个元组列表
Enum.zip list, [:a, :b, :c]
Enum.with_index ["noce", "upon", "a", "time"]

# 将收集中的元素归约为一个值
Enum.reduce 1..100, &(&1 + &2)
Enum.reduce(["now", "is", "the", "time"],
            fn word, longest ->
              if String.length(word) > String.length(longest) do
                word
              else
                longest
              end
            end
)
Enum.reduce(["now", "is", "the", "time"], 0, fn word, longest ->
            if String.length(word) > longest do
              String.length(word)
            else
              longest
            end
end)

# 洗一手扑克牌
deck = for rank <- '23456789TJQKA', suit <- 'CDHS', do: [suit, rank]
deck |> shuffle |> take(13)
hands = deck |> shuffle |> chunk(13)
```

* 习题：ListsAndRecursion-5

```elixir
# ListsAndRecursion-5.exs
defmodule MyEnum do
  def filter([head | tail], func) do
    if func.(head) do
      [head | filter(tail, func)]
    else
      filter(tail, func)
    end
  end
  def filter([], _func), do: ''
end
```

* 习题： ListsAndRecursion-6

```elixir
# codes/ListsAndRecursion-6.exs
defmodule MyList do
  def flatten([]), do: []
  def flatten([head | tail]), do: flatten(head) ++ flatten(tail)
  def flatten(head), do: [head]
end
```

## Stream——延迟处理的枚举类型

在Elixir中Enum是贪婪的，它会计算传递给它的收集的所有内容。看看如下的管道：

```elixir
# codes/pipeline.exs
[1, 2, 3, 4, 5]
|> Enum.map(&(&1*&1))
|> Enum.with_index
|> Enum.map(fn {value, index} -> value - index end)
|> IO.inspect
```
管道输出最终结果的过程中产生了4个列表。再看下段代码，从文件中读取多行内容并返回最长的那一行

```elixir
# codes/longest_word.exs
IO.puts File.read!("pipeline.exs")
        |> String.split
        |> Enum.max_by(&String.length/1)
```

上面的代码都不是最优的，因为每次调用Enum都是独立的，每次调用接受一个收集并返回一个收集。

我们真正想要的是在需要的时候才处理在收集里的元素。我们不需要存储包含整个收集的中间结果，仅需要在函数之间传递当前元素，这正是流stream的作用。

### 流是可组合的枚举器

下面是一个创建Stream的简单例子：

```elixir
s = Stream.map [1, 3, 5, 7], &(&1 + 1)
```

如何让流开始返回结果，把它当做收集，将它传递给Enum模块的某个函数：

```elixir
Enum.to_list s
```

也可以将流传递给流函数。流是可组合的。

```elixir
# codes/stream1.exs
[head | tail] =
  [1, 2, 3, 4]
  |> Stream.map(fn x -> x * x end)
  |> Stream.map(fn x -> x + 1 end)
  |> Stream.filter(fn x -> rem(x, 2) == 1 end)
  |> Enum.to_list()
```

```elixir
# codes/stream2.exs
IO.puts(
  File.open!("./../README.md")
  |> IO.stream(:line)
  |> Enum.max_by(&String.length/1)
)
```
连续的数据行抵达的很慢但是一直持续不断，使用Enum则必须等所有的数据行都抵达才能开始处理
，使用流则只要有数据抵达就对它进行处理。

### 无限流

由于流是延迟处理的，无须提前获得整个收集。对比下面两行代码运行速度就能知道差异：

  ```elixir
    Enum.map(1..10_000_000, &(&1 + 1)) |> Enum.take(5)
    Stream.map(1..10_000_000, &(&1 + 1)) |> Enum.take(5)
  ```
Enum.map创建了一个包含1000万个元素的列表，而仅需取得前五个数字，所以这种操作是不合适的，
使用Stream则无需获得整个收集。

### 自定义流

#### Stream.cycle

接受一个枚举类型参数，并返回一个包含该枚举类型参数元素的无限流。

  ```elixir
    Stream.cycle(~w{ green white })
      |> Stream.zip(1..5)
      |> Enum.map(fn {class, value} -> ~s{<tr class="#{class}"><td>#{value}</td></tr>\n} end)
      |> IO.puts
  ```

#### Stream.repeatedly

接受一个函数，并在需要一个新值的时候调用该函数。

  ```elixir
    Stream.repeatedly(fn -> true end) |> Enum.take(3)
    # Erlang的rand模块替代了random
    Stream.repeatedly(&:random.uniform/0) |> Enum.take(3)
  ```

#### Stream.iterate

Stream.iterate(start_value, next_fun)生成一个无限流。第一个值是start_value，
下一个值是调用next_fun函数以第一个值作为参数生成的。

```elixir
Stream.iterate(0, &(&1 + 1)) |> Enum.take(5)
Stream.iterate(2, &(&1 * &1)) |> Enum.take(5)
Stream.iterate([], &[&1]) |> Enum.take(5)
```

#### Stream.unfold

Stream.unfold类似于iterate，但是能分理出传递给流的值和传递给下一次迭代的值。
提供一个初始值和函数，函数使用这个初始值产生两个值，作为一个元组返回。第一个
值作为流的当前迭代值而返回，第二个值传递给函数，用于流的下一次迭代。如果函数
返回nil则流就终止。

```elixir
Stream.unfold({0, 1}, fn {f1, f2} -> {f1, {f2, f1 + f2}} end) |> Enum.take(15)
```

#### Stream.resource

```elixir
Stream.resource(fn -> File.open("./../README.md") end,
                fn file ->
                  case IO.read(file, :line) do
                    line when is_binary(line) -> { [line], file }
                    _ -> {:halt, file}
                    end
                  end,
                  fn file -> File.close(file) end)
```

```elixir
# codes/countdown.exs
defmodule Countdown do
  def sleep(seconds) do
    receive do
    after seconds * 1000 -> nil
    end
  end

  def say(text) do
    spawn fn -> :os.cmd('say #{text}') end
  end

  def timer do
    Stream.resource(
      fn ->
        {_h, _m, s} = :erlang.time
        60 - s - 1
      end,

      fn
        0 ->
          {:halt, 0}

        count ->
          sleep(1)
          {[inspect(count)], count - 1}
      end,

      fn _ -> end # 不需要释放任何东西
    )
  end
end
```

```elixir
counter = Countdown.timer
printer = counter |> Stream.each(&IO.puts/1)
speaker = printer |> Stream.each(&Countdown.say/1)

speaker |> Enum.take(5)
```

## Collectable 协议

Enumerable协议让你可以遍历一个类型中的元素，给定一个收集，可以获得每个元素。
collectable正好相反，它允许你通过插入元素构建一个收集。

```elixir
Enum.into 1..5, []
Enum.into 1..5, [100, 101]
Enum.into IO.stream(:stdio, :line), IO.stream(:stdio, :line)
```

## 推导式

给定一个或多个收集，从每个收集中提取所有值的组合，选择性的过滤某些值，然后使用剩下的值
生成一个新的集合。

通用语法：
```
result = for generator or filter ... [, into: value], do :expression
```

先看几个例子：

```elixir
for x <- [1, 2, 3, 4, 5], do: x * x
for x <- [1, 2, 3, 4, 5], x < 4, do: x * x
for x <- [1, 2], y <- [5, 6], do: x * y
for x <- [1, 2], y <- [5, 6], do: {x, y}
```

后面的生成器可以使用前面生成器的变量：

```elixir
min_maxes = [{1, 4}, {2, 3}, {10, 15}]
for {min, max} <- min_maxes, n <- min .. max, do: n
```
过滤器是一种断言，如果条件为假，推导式不会输出该值，而是转到下一个迭代。

```elixir
first8 = [1, 2, 3, 4, 5, 6, 7, 8]
for x <- first8, y <- first8, x >= y, rem(x * y, 10) == 0, do: {x, y}
```

由于生成器的第一项是一个模式，可以用来解构结构化数据，这里有一个用来交换关键字列表的键和值的推导式。

```elixir
reports = [dallars: :hot, minneapolis: :cold, dc: :muggy, la: :smoggy]
for {city, weather} <- reports, do: {weather, city}
```

### 推导式也可以处理二进制位

```elixir
for << ch <- "hello" >>, do: ch
for << ch <- "hello" >>, do: <<ch>>
for << <<b1::size(2), b2::size(3), b3::size(3)>> <- "hello">>,
  do: "0#{b1}#{b2}#{b3}"
```

### 作用域与推导式

所有推导式内部赋值的变量只在推导式内部有效，不会对作用域外部的变量产生影响。

### 推导式的返回值

在迄今为止例子中，推导式都返回列表，列表包含推导式的do表达式每次迭代的返回值。
此行为可被into:参数改变。它接受一个收集，该收集接受推导式的结果。例如，我们可以
填充散列表：

```elixir
for x <- ~w{cat, dog}, into: %{}, do: {x, String.upcase(x)}
for x <- ~w{cat, dog}, into: %{"ant": "ANT"}, do: {x, String.upcase(x)}
```

* 习题：ListsAndRecursion-7

```elixir
# codes/ListsAndRecursion-7.exs
defmodule MyList do
  def span(from, to) when from == to,
    do: [from]

  def span(from, to) when from < to,
    do: [from | span(from + 1, to)]

  def span(from, to) when from > to,
    do: [from | span(from - 1, to)]

  def all?([], _), do: true

  def all?([head | tail], func) do
    if func.(head) do
      all?(tail, func)
    else
      false
    end
  end

  def primes_to(to) do
    request_range = span(2, to)
    for x <- request_range, is_prime?(x), do: x
  end

  # prime is if a number when divided by any number up to itself always leaves a remainder.
  defp is_prime?(number),
    do: all?(Enum.into(2..(number - 1), []), &(rem(number, &1) != 0))
end
```

* 习题：ListsAndRecursion-8

```elixir
#codes/ListsAndRecursion-8.exs
defmodule PB do
  def make_total(tax_rates, orders) do
    for each_order <- orders, do: do_make(tax_rates, each_order)
  end

  def do_make(tax_rates, each_order)  do
    [_, {_, location}, {_, net_amount }] = each_order

    if Enum.any? tax_rates, fn({city, _rate}) -> city == location end do
      {_, rate} = Enum.find(tax_rates, fn {l, _p} -> l == location end)
      Enum.into [total_amount: net_amount * (1 + rate)], each_order
    else
      each_order
    end
  end
end
```
